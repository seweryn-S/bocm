#!ipxe

# Jesli w systemie wystepuje wiele kart sieciowych nalezy wskazac ta z ktorej ma startowac system
# set BOOTIF 01-MAC_ADDRESS
#set BOOTIF 01-3c-ec-ef-af-58-bc

# Podlaczenie SSH do initramfs-a
# ssh -p 2222 -oHostKeyAlgorithms=ssh-rsa -oPubkeyAcceptedKeyTypes=+ssh-rsa root@adres_serwera

# Mozliwe parametry i ich znaczenie
# img_uri - URL wskazujacy na obraz systemu, wraz z plikiem obrazu
# cfg_uri - URL wskazujacy na katalog z konfiguracja systemu
# make_volumes - czysci dysk wskazany jako startory w partition.yml, oraz tworzy od nowa cala strukture
# disk_info - automatycznie wywoluje funkcje disk_info wyswietlajaco informacje o zainstalowanych dyskach
#
# break - parametr sluzacy do awaryjnego tymczasowego zatrzymania przetwarzania skryptow w danym momencie
# Mozliwe wartosci parametru break, sluzace do diagnostyki
#   before_net_config
#   after_net_config - Uwaga od tego parametru mozna sie logowac przez SSH na port 2222
#   before_bocm_top
#   before_user_bottom
#   after_user_bottom
#   end

set TEMPLATE ubuntu22.04
set TEMP_VER latest
set BREAK after_user_bottom

menu iPXE boot menu
item --gap -- ---------------- Oparating systems ----------------
item hdd        Boot from next devices
item def_opt   ${TEMPLATE}-${TEMP_VER}
item opt_mv      ${TEMPLATE}-${TEMP_VER} - make_volumes break=${BREAK}
item disk_info  Disk info
item --gap -- ---------------- Advanced configuration ----------------
item shell      Drop to iPXE shell
item exit       Exit to BIOS boot
choose --default def_opt --timeout 10000 target && goto ${target} || reboot

:hdd
# Boot the first local HDD
#sanboot --no-describe --drive 0x80
# Boot fron next devices
exit

:disk_info
set PARAMS disk_info
goto image

:def_opt
set PARAMS
goto image

:opt_mv
set PARAMS break=${BREAK} make_volumes
goto image

:image
set TEMP_COMP .tzst
goto base

:base
set INITRD initrd.img # Uwaga trzeba te ustawic pasujacy do ramdysku kernel
set BASEURL https://boipxe/templates
set BOCMDIR ${BASEURL}/bocm-v1.0.0
set IMG_URI ${BASEURL}/${TEMPLATE}/${TEMPLATE}-${TEMP_VER}${TEMP_COMP}
set CFG_URI ${BASEURL}/CONFIGS/${hostname}
set KERNEL_PARAMS loglevel=3 initrd=${INITRD} net.ifnames=0 biosdevname=0 ipv6.disable=1 ip=dhcp rw -- img_uri=${IMG_URI} cfg_uri=${CFG_URI} ${PARAMS}
# Jesli zdefiniowana zmienna BOOTIF
#set KERNEL_PARAMS loglevel=3 initrd=${INITRD} net.ifnames=0 biosdevname=0 ipv6.disable=1 ip=dhcp rw -- BOOTIF=${BOOTIF} img_uri=${IMG_URI} cfg_uri=${CFG_URI} ${PARAMS}
goto sysboot

:sysboot
echo 'Loading kernel ...'
kernel ${BOCMDIR}/vmlinuz root=LABEL=lvroot ${KERNEL_PARAMS}
echo 'Loading initial ramdisk ...'
initrd ${BOCMDIR}/${INITRD}
boot

:shell
shell