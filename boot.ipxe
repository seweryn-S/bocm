#!ipxe

# Jesli w systemie wystepuje wiele kart sieciowych nalezy wskazac ta z ktorej ma startowac system
# set BOOTIF 01-MAC_ADDRESS
#set BOOTIF 01-3c-ec-ef-af-58-bc

# Mozliwe parametry i ich znaczenie
# img_uri - URL wskazujacy na obraz systemu, wraz z plikiem obrazu
# cfg_uri - URL wskazujacy na katalog z konfiguracja systemu
# make_volumes - czysci dysk wskazany jako startory w partition.yml, oraz tworzy od nowa cala strukture
# disk_info - automatycznie wywoluje funkcje disk_info wyswietlajaco informacje o zainstalowanych dyskach
#
# break - parametr sluzacy do awaryjnego tymczasowego zatrzymania przetwarzania skryptow w danym momencie
# Mozliwe wartosci parametru break, sluzace do diagnostyki
#   before_net_config
#   after_net_config
#   before_bocm_top
#   before_user_bottom
#   after_user_bottom
#   end

menu iPXE boot menu
item --gap -- ---------------- Oparating systems ----------------
item hdd        Boot from next devices
item ubuntu2004   Ubuntu20.04:latest
item ubuntu2004_mv      Ubuntu20.04:latest - make_volumes break=end
item disk_info  Disk info
item --gap -- ---------------- Advanced configuration ----------------
item shell      Drop to iPXE shell
item exit       Exit to BIOS boot
choose --default ubuntu2004 --timeout 10000 target && goto ${target} || reboot

:hdd
# Boot the first local HDD
#sanboot --no-describe --drive 0x80
# Boot fron next devices
exit

:disk_info
set TEMPLATE ubuntu18.04dev
set PARAMS disk_info
goto base

:ubuntu2004
set TEMPLATE ubuntu20.04
set PARAMS
goto base

:ubuntu2004_mv
set TEMPLATE ubuntu20.04
set PARAMS break=end make_volumes
goto base

:base
set BOCMDIR ${dhcp-server}/templates/bocm-v1.0.0-dev
set IMG_URI ${dhcp-server}/templates/${TEMPLATE}/${TEMPLATE}-1.5.0.tgz
set CFG_URI ${dhcp-server}/templates/CONFIGS/${hostname}
set KERNEL_PARAMS loglevel=3 initrd=initrd.img net.ifnames=0 biosdevname=0 ipv6.disable=1 ip=dhcp rw -- img_uri=${IMG_URI} cfg_uri=${CFG_URI} ${PARAMS}
# Jesli zdefiniowana zmienna BOOTIF
#set KERNEL_PARAMS loglevel=3 initrd=initrd.img net.ifnames=0 biosdevname=0 ipv6.disable=1 ip=dhcp rw -- BOOTIF=${BOOTIF} img_uri=${IMG_URI} cfg_uri=${CFG_URI} ${PARAMS}
goto sysboot

:sysboot
echo 'Loading kernel ...'
kernel http://${BOCMDIR}/vmlinuz root=LABEL=lvroot ${KERNEL_PARAMS}
echo 'Loading initial ramdisk ...'
initrd http://${BOCMDIR}/initrd.img
boot

:shell
shell